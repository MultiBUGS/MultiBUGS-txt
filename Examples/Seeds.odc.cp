	Seeds: Random effect logistic
			regression

This example is taken from Table 3 of Crowder (1978), and concerns the proportion of seeds that germinated on each of 21 plates arranged according to a 2 by 2 factorial layout by seed and type of root extract.   The data are shown below, where ri and ni are the number of germinated and the total number of seeds on the i th plate, i =1,...,N.  These data are also analysed by, for example, Breslow: and Clayton (1993). 


		seed O. aegyptiaco 75						seed O. aegyptiaco 73
		Bean			Cucumber			Bean			Cucumber
			r	n	r/n	r	n	r/n	r	n	r/n	r	n	r/n
	_________________________________________________________________
	10	39	0.26	5	6	0.83	8	16	0.50	3	12	0.25
	23	62	0.37	53	74	0.72	10	30	0.33	22	41	0.54	
	23	81	0.28	55	72	0.76	8	28	0.29	15	30	0.50
	26	51	0.51	32	51	0.63	23	45	0.51	32	51	0.63
	17	39	0.44	46	79	0.58	0	4	0.00	3	7	0.43
				10	13	0.77	

The model is essentially a random effects logistic, allowing for over-dispersion.  If pi is the probability of germination on the i th plate,  we assume

	ri  ~  Binomial(pi, ni)
	
	logit(pi) = a0 + a1x1i + a2x2i + a12x1ix2i + bi
	
	bi  ~ Normal(0, t)
	
where x1i  , x2i   are the seed type and root extract of the i th plate, and an interaction term a12x1ix2i   is included.   a0 , a1 ,  a2 ,   a12 ,   t  are given independent "noninformative" priors.  

Graphical model for seeds  example

 
BUGS language for seeds example

	
		model
		{
			for( i in 1 : N ) {
				r[i] ~ dbin(p[i],n[i])
				beta[i] ~ dnorm(0.0,tau)
				logit(p[i]) <- alpha0 + alpha1 * x1[i] + alpha2 * x2[i] + 
					alpha12 * x1[i] * x2[i] + beta[i]
			}
			alpha0 ~ dnorm(0.0,1.0E-6)
			alpha1 ~ dnorm(0.0,1.0E-6)
			alpha2 ~ dnorm(0.0,1.0E-6)
			alpha12 ~ dnorm(0.0,1.0E-6)
			sigma ~ dunif(0,10)
			tau <- 1 / pow(sigma, 2)
		}


Data ( click to open )

Inits for chain 1 	Inits for chain 2	( click to open )


Results

		mean	median	sd	MC_error	val2.5pc	val97.5pc	start	sample	ESS
	alpha0	-0.5529	-0.5529	0.2138	0.003881	-0.9749	-0.1273	1001	20000	3035
	alpha1	0.06533	0.07175	0.3446	0.005596	-0.6292	0.729	1001	20000	3792
	alpha12	-0.8291	-0.8242	0.4762	0.008243	-1.768	0.119	1001	20000	3337
	alpha2	1.36	1.36	0.3065	0.006007	0.7608	1.992	1001	20000	2602
	sigma	0.3472	0.3365	0.15	0.004845	0.07822	0.6816	1001	20000	958

We may compare simple logistic, maximum likelihood (from EGRET), penalized quasi-likelihood  (PQL) Breslow and Clayton (1993) with the BUGS results




Heirarchical centering is an interesting reformulation of random effects models. Introduce the variables

	mi  = a0 + a1x1i + a2x2i + a12x1ix2i  

	bi  =  mi + bi

the model then becomes

	ri  ~  Binomial(pi, ni)

	logit(pi) = bi

	bi  ~ Normal(mi , t)

The graphical model is shown below	


	
	model
	{
		for( i in 1 : N ) {
			b[i] ~ dnorm(mu[i], tau)
			r[i] ~ dbin(p[i], n[i])
			mu[i] <- alpha0 + alpha1 * x1[i] + alpha2 * x2[i] + alpha12 * x1[i] * x2[i]
			logit(p[i]) <- b[i]
		}
		alpha0 ~ dnorm(0.0, 1.0E-6)
		alpha1 ~ dnorm(0.0, 1.0E-6)
		alpha12 ~ dnorm(0.0, 1.0E-6)
		alpha2 ~ dnorm(0.0, 1.0E-6)
		tau ~ dunif(0,10)
		sigma <- 1 / pow(tau, 2)
	}

Results	


		mean	median	sd	MC_error	val2.5pc	val97.5pc	start	sample	ESS
	alpha0	-0.5516	-0.5482	0.2268	0.005893	-1.003	-0.1158	1001	20000	1481
	alpha1	0.04667	0.04353	0.3566	0.009616	-0.6501	0.7495	1001	20000	1375
	alpha12	-0.8469	-0.8397	0.5071	0.01358	-1.865	0.1212	1001	20000	1393
	alpha2	1.376	1.372	0.3228	0.008237	0.7544	2.029	1001	20000	1536
	sigma	0.03174	0.01955	0.06359	8.063E-4	0.01028	0.1243	1001	20000	6220

This formulation of the model has two advantages: the squence of random numbers generated by the Gibbs sampler has better correlation properties and the time per update is reduced because the updating for the a parameters is now conjugate.
