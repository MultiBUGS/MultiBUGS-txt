	Seeds: Random effect logistic
			regression

This example is taken from Table 3 of Crowder (1978), and concerns the proportion of seeds that germinated on each of 21 plates arranged according to a 2 by 2 factorial layout by seed and type of root extract.   The data are shown below, where ri and ni are the number of germinated and the total number of seeds on the i th plate, i =1,...,N.  These data are also analysed by, for example, Breslow: and Clayton (1993). 


		seed O. aegyptiaco 75						seed O. aegyptiaco 73
		Bean			Cucumber			Bean			Cucumber
			r	n	r/n	r	n	r/n	r	n	r/n	r	n	r/n
	_________________________________________________________________
	10	39	0.26	5	6	0.83	8	16	0.50	3	12	0.25
	23	62	0.37	53	74	0.72	10	30	0.33	22	41	0.54	
	23	81	0.28	55	72	0.76	8	28	0.29	15	30	0.50
	26	51	0.51	32	51	0.63	23	45	0.51	32	51	0.63
	17	39	0.44	46	79	0.58	0	4	0.00	3	7	0.43
				10	13	0.77	

The model is essentially a random effects logistic, allowing for over-dispersion.  If pi is the probability of germination on the i th plate,  we assume

	ri  ~  Binomial(pi, ni)
	
	logit(pi) = a0 + a1x1i + a2x2i + a12x1ix2i + bi
	
	bi  ~ Normal(0, t)
	
where x1i  , x2i   are the seed type and root extract of the i th plate, and an interaction term a12x1ix2i   is included.   a0 , a1 ,  a2 ,   a12 ,   t  are given independent "noninformative" priors.  

Graphical model for seeds  example

 
BUGS language for seeds example

	
		model
		{
			for( i in 1 : N ) {
				r[i] ~ dbin(p[i],n[i])
				beta[i] ~ dnorm(0.0,tau)
				logit(p[i]) <- alpha0 + alpha1 * x1[i] + alpha2 * x2[i] + 
					alpha12 * x1[i] * x2[i] + beta[i]
			}
			alpha0 ~ dnorm(0.0,1.0E-6)
			alpha1 ~ dnorm(0.0,1.0E-6)
			alpha2 ~ dnorm(0.0,1.0E-6)
			alpha12 ~ dnorm(0.0,1.0E-6)
			tau ~ dgamma(0.001,0.001)
			sigma <- 1 / sqrt(tau)
		}


Data ( click to open )

Inits for chain 1 	Inits for chain 2	( click to open )


Results

		mean	median	sd	MC_error	val2.5pc	val97.5pc	start	sample	ESS
	alpha0	-0.5483	-0.5485	0.1947	0.002994	-0.9343	-0.1603	1001	20000	4229
	alpha1	0.07528	0.08268	0.319	0.004681	-0.5853	0.6815	1001	20000	4645
	alpha12	-0.8142	-0.8095	0.4339	0.006099	-1.674	0.04339	1001	20000	5060
	alpha2	1.351	1.348	0.2721	0.004318	0.8185	1.901	1001	20000	3970
	sigma	0.2898	0.2818	0.1441	0.004869	0.04794	0.6008	1001	20000	875

We may compare simple logistic, maximum likelihood (from EGRET), penalized quasi-likelihood  (PQL) Breslow and Clayton (1993) with the BUGS results




Heirarchical centering is an interesting reformulation of random effects models. Introduce the variables

	mi  = a0 + a1x1i + a2x2i + a12x1ix2i  

	bi  =  mi + bi

the model then becomes

	ri  ~  Binomial(pi, ni)

	logit(pi) = bi

	bi  ~ Normal(mi , t)

The graphical model is shown below	


	
	model
	{
		for( i in 1 : N ) {
			b[i] ~ dnorm(mu[i], tau)
			r[i] ~ dbin(p[i], n[i])
			mu[i] <- alpha0 + alpha1 * x1[i] + alpha2 * x2[i] + alpha12 * x1[i] * x2[i]
			logit(p[i]) <- b[i]
		}
		alpha0 ~ dnorm(0.0, 1.0E-6)
		alpha1 ~ dnorm(0.0, 1.0E-6)
		alpha12 ~ dnorm(0.0, 1.0E-6)
		alpha2 ~ dnorm(0.0, 1.0E-6)
		tau ~ dgamma(0.001, 0.001)
		sigma <- 1 / sqrt(tau)
	}

Results	


		mean	median	sd	MC_error	val2.5pc	val97.5pc	start	sample	ESS
	alpha0	-0.5626	-0.5589	0.1942	0.005895	-0.9487	-0.1802	1001	20000	1085
	alpha1	0.09654	0.09662	0.3154	0.01069	-0.5489	0.7114	1001	20000	870
	alpha12	-0.8441	-0.8254	0.438	0.01427	-1.735	-0.01408	1001	20000	941
	alpha2	1.367	1.362	0.2757	0.008512	0.8488	1.951	1001	20000	1049
	sigma	0.2856	0.2757	0.1438	0.005038	0.04762	0.6002	1001	20000	814

This formulation of the model has two advantages: the squence of random numbers generated by the Gibbs sampler has better correlation properties and the time per update is reduced because the updating for the a parameters is now conjugate.
