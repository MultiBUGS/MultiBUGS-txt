	Seeds: Random effect logistic
			regression

This example is taken from Table 3 of Crowder (1978), and concerns the proportion of seeds that germinated on each of 21 plates arranged according to a 2 by 2 factorial layout by seed and type of root extract.   The data are shown below, where ri and ni are the number of germinated and the total number of seeds on the i th plate, i =1,...,N.  These data are also analysed by, for example, Breslow: and Clayton (1993). 


		seed O. aegyptiaco 75						seed O. aegyptiaco 73
		Bean			Cucumber			Bean			Cucumber
			r	n	r/n	r	n	r/n	r	n	r/n	r	n	r/n
	_________________________________________________________________
	10	39	0.26	5	6	0.83	8	16	0.50	3	12	0.25
	23	62	0.37	53	74	0.72	10	30	0.33	22	41	0.54	
	23	81	0.28	55	72	0.76	8	28	0.29	15	30	0.50
	26	51	0.51	32	51	0.63	23	45	0.51	32	51	0.63
	17	39	0.44	46	79	0.58	0	4	0.00	3	7	0.43
				10	13	0.77	

The model is essentially a random effects logistic, allowing for over-dispersion.  If pi is the probability of germination on the i th plate,  we assume

	ri  ~  Binomial(pi, ni)
	
	logit(pi) = a0 + a1x1i + a2x2i + a12x1ix2i + bi
	
	bi  ~ Normal(0, t)
	
where x1i  , x2i   are the seed type and root extract of the i th plate, and an interaction term a12x1ix2i   is included.   a0 , a1 ,  a2 ,   a12 ,   t  are given independent "noninformative" priors.  

Graphical model for seeds  example

 
BUGS language for seeds example

	
		model
		{
			for( i in 1 : N ) {
				r[i] ~ dbin(p[i],n[i])
				b[i] ~ dnorm(0.0,tau)
				logit(p[i]) <- alpha0 + alpha1 * x1[i] + alpha2 * x2[i] + 
					alpha12 * x1[i] * x2[i] + b[i]
			}
			alpha0 ~ dnorm(0.0,1.0E-6)
			alpha1 ~ dnorm(0.0,1.0E-6)
			alpha2 ~ dnorm(0.0,1.0E-6)
			alpha12 ~ dnorm(0.0,1.0E-6)
			tau ~ dgamma(0.001,0.001)
			sigma <- 1 / sqrt(tau)
		}


Data ( click to open )

Inits for chain 1 	Inits for chain 2	( click to open )


Results

		mean	median	sd	MC_error	val2.5pc	val97.5pc	start	sample	ESS
	alpha0	-0.5503	-0.5496	0.1985	0.00333	-0.9515	-0.1573	1001	20000	3554
	alpha1	0.07863	0.08912	0.3202	0.004819	-0.5818	0.6902	1001	20000	4415
	alpha12	-0.8145	-0.8162	0.4441	0.006201	-1.714	0.06173	1001	20000	5129
	alpha2	1.349	1.346	0.2804	0.004677	0.7992	1.921	1001	20000	3594
	sigma	0.2893	0.2807	0.1469	0.005471	0.04131	0.6097	1001	20000	721

We may compare simple logistic, maximum likelihood (from EGRET), penalized quasi-likelihood  (PQL) Breslow and Clayton (1993) with the BUGS results




Heirarchical centering is an interesting reformulation of random effects models. Introduce the variables

	mi  = a0 + a1x1i + a2x2i + a12x1ix2i  

	bi  =  mi + bi

the model then becomes

	ri  ~  Binomial(pi, ni)

	logit(pi) = bi

	bi  ~ Normal(mi , t)

The graphical model is shown below	


	
	model
	{
		for( i in 1 : N ) {
			beta[i] ~ dnorm(mu[i], tau)
			r[i] ~ dbin(p[i], n[i])
			mu[i] <- alpha0 + alpha1 * x1[i] + alpha2 * x2[i] + alpha12 * x1[i] * x2[i]
			logit(p[i]) <- beta[i]
		}
		alpha0 ~ dnorm(0.0, 1.0E-6)
		alpha1 ~ dnorm(0.0, 1.0E-6)
		alpha12 ~ dnorm(0.0, 1.0E-6)
		alpha2 ~ dnorm(0.0, 1.0E-6)
		tau ~ dgamma(0.001, 0.001)
		sigma <- 1 / sqrt(tau)
	}

Results	

		mean	median	sd	MC_error	val2.5pc	val97.5pc	start	sample	ESS
	alpha0	-0.5469	-0.548	0.1939	0.00556	-0.9339	-0.1504	1001	20000	1215
	alpha1	0.07719	0.09023	0.3176	0.009796	-0.5854	0.6696	1001	20000	1051
	alpha12	-0.8264	-0.82	0.4345	0.01341	-1.676	0.03514	1001	20000	1050
	alpha2	1.349	1.345	0.2695	0.00783	0.8164	1.912	1001	20000	1184
	sigma	0.2864	0.2797	0.1429	0.005025	0.04379	0.5962	1001	20000	809


This formulation of the model has two advantages: the squence of random numbers generated by the Gibbs sampler has better correlation properties and the time per update is reduced because the updating for the a parameters is now conjugate.
